plugins {
	id "java-base"
}

description = "Spring Boot Ant smoke test"

configurations {
	antDependencies {
		extendsFrom dependencyManagement
	}
	testRepository
}

sourceSets {
	test
}

plugins.withType(EclipsePlugin) {
	eclipse {
		classpath {
			plusConfigurations = [configurations.testRuntimeClasspath]
		}
	}
}

dependencies {
	antDependencies "org.apache.ivy:ivy:2.5.0"
	antDependencies project(path: ":spring-boot-project:spring-boot-tools:spring-boot-antlib")
	antDependencies "org.apache.ant:ant-launcher:1.10.7"
	antDependencies "org.apache.ant:ant:1.10.7"

	testRepository(project(path: ":spring-boot-project:spring-boot-tools:spring-boot-loader", configuration: "mavenRepository"))
	testRepository(project(path: ":spring-boot-project:spring-boot-starters:spring-boot-starter", configuration: "mavenRepository"))

	testImplementation(project(":spring-boot-project:spring-boot-tools:spring-boot-loader-tools"))
	testImplementation(project(":spring-boot-project:spring-boot-tools:spring-boot-test-support"))
	testImplementation("org.assertj:assertj-core")
	testImplementation("org.junit.jupiter:junit-jupiter")
	testImplementation("org.springframework:spring-core")
}

def syncTestRepository = tasks.register("syncTestRepository", Sync) {
	destinationDir = file(layout.buildDirectory.dir("test-repository"))
	from configurations.testRepository
	rename {
		it.replaceAll("-[0-9]+\\.[0-9]+-[0-9]+\\.", "-SNAPSHOT.")
	}
}

def syncAntSources = tasks.register("syncAntSources", Sync) {
	destinationDir file(layout.buildDirectory.dir("ant"))
	from project.layout.projectDirectory
	include "*.xml"
	filter(springRepositoryTransformers.ant())
}

def antRun = tasks.register("antRun", JavaExec) {
	workingDir layout.buildDirectory.dir("ant")
	classpath = configurations.antDependencies
	mainClass = "org.apache.tools.ant.launch.Launcher"
	args = [ "clean", "build" ]
	systemProperties = [
		"ant-spring-boot.version" : version,
		//todo this should be a CommandLineArgumentProvider, or antRun should be a proper task class
		"projectDir": project.layout.projectDirectory.asFile.absolutePath
	]
	inputs.dir(syncTestRepository.map { it.destinationDir })
		.withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.dir(syncAntSources.map { it.destinationDir })
		.withPathSensitivity(PathSensitivity.RELATIVE)
	outputs.dir(layout.buildDirectory.dir("ant"))
}

def test = tasks.register("test", Test) {
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	inputs.dir(antRun.map { it.outputs.files.singleFile })
			.withPathSensitivity(PathSensitivity.RELATIVE)
}

tasks.named("check") {
	dependsOn(test)
}
